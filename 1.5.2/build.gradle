buildscript {
    repositories {
        maven { url snapshotRepoUrl }
        maven { url releaseRepoUrl }
        mavenLocal()
    }
}

plugins {
    id "java"
    id "maven-publish"
}

repositories {
    maven { url snapshotRepoUrl }
    maven { url releaseRepoUrl }
    mavenLocal()
}

ext {
    buildNumber = project.hasProperty("buildNumber") ? "-" + project.getProperty("buildNumber") : ""
    mailSpecVersion = "1.5"
}

project.configure(project) {
    String GRADLE_PROPS_LOCATION = "/home/devtools/.m2/gradle-user.txt"
    String LOCAL_GRADLE_PROPS_LOCATION = "${System.env.HOME}/.m2/gradle-user.txt"
    File gradlePropsFile = new File(GRADLE_PROPS_LOCATION)

    if (!gradlePropsFile.exists()) {
        gradlePropsFile = new File(LOCAL_GRADLE_PROPS_LOCATION)
    }

    ext.publisherUsername = null
    ext.publisherPassword = null

    Properties publisherProps = new Properties()
    if (gradlePropsFile.exists()) {
        gradlePropsFile.withInputStream {
            stream -> publisherProps.load(stream)
        }
        ext.publisherUsername = publisherProps.getProperty("publisherUsername")
        ext.publisherPassword = publisherProps.getProperty("publisherPassword")
    } else {
        println "WARNING: If you want to publish to nexus, ${GRADLE_PROPS_LOCATION} or ${LOCAL_GRADLE_PROPS_LOCATION} must exist and contain two properties: publisherUsername and publisherPassword"
    }
}

sourceSets.main.java.srcDirs = [
    "src/main/java",
    "${buildDir}/generated-source"
]

targetCompatibility = "1.7"
sourceCompatibility = "1.7"

publishing {
    repositories {
        maven {
            url = releasePublisherUrl

            credentials {
                username "${project.ext.publisherUsername}"
                password "${project.ext.publisherPassword}"
            }
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}

jar {
    manifest {
        attributes (
            "Extension-Name": "javax.mail",
            "Specification-Title": "JavaMail(TM) API Design Specification",
            "Specification-Version": "${mailSpecVersion}",
            "Specification-Vendor": "Oracle",
            "Implementation-Title": "javax.mail",
            "Implementation-Version": "${version}",
            "Implementation-Vendor": "Oracle",
            "Implementation-Vendor-Id": "com.sun"
        )
    }

    exclude("**/Version.java")
}

javadoc {
    // avoid java8 doclint errors - without the -quiet option, the build fails
    options.addStringOption("Xdoclint:none", "-quiet")
}

task sourceJar(type: Jar) {
    classifier "sources"
    from ("src/main/java")
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

task generateSource(type: Copy) {
    from file("src/main/resources/javax/mail/Version.java")
    into file("${buildDir}/generated-source/javax/mail")
    filter { line -> line.replaceAll("\\\$\\{mail.version\\}", "${version}") }

    tasks.findByName("compileJava").dependsOn "generateSource"
}
